// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // load reg?
    Or(a = reset, b = load, out = ora);
    Or(a = ora, b = inc, out = loadres);

    // counter inc
    Inc16(in = regout, out = incout);

    // inc
    Mux16(a = regout, b = incout, sel = inc, out = toload);
    // load
    Mux16(a = toload, b = in, sel = load, out = toreset);
    // if reset
    Mux16(a = toreset, sel = reset, out = combout);

    // register
    Register(in = combout, load = loadres, out = regout, out = out);
}
